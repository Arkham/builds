# Maintainer: Arkham <arkham at archlinux dot us>
# Contributor: Manuel "ekerazha" C. (www.ekerazha.com)

pkgname=cairo-lcd
_realname=cairo
pkgver=1.8.8
pkgrel=1
pkgdesc="Cairo vector graphics library (with custom LCD filter)."
arch=('i686' 'x86_64')
url="http://cairographics.org/"
license=('LGPL' 'MPL')
depends=('libpng>=1.2.33' 'libxrender' 'fontconfig>=2.6.0' 'pixman>=0.12.0' 'xcb-util>=0.3.2')
makedepends=('pkgconfig')
optdepends=('fontconfig-lcd: automatically configure LCD filter')
conflicts=('cairo')
provides=("$_realname=$pkgver")
options=('!libtool')
source=(http://cairographics.org/releases/cairo-$pkgver.tar.gz 
        lcd-filter.patch)
md5sums=('d3e1a1035ae563812d4dd44a74fb0dd0'
         '50e5aaea4c3f347c48eb2589938f99a8')

build() {
    cd $srcdir/$_realname-$pkgver

    # enable LCD filter (http://bugs.freedesktop.org/show_bug.cgi?id=10301)
    patch -Np1 -i ../lcd-filter.patch || return 1

    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --enable-xcb --disable-static || return 1
    make || return 1
    make DESTDIR=$pkgdir install || return 1
}
