# Maintainer: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=bleachbit
pkgver=0.7.0
pkgrel=2
pkgdesc="Deletes unneeded files to free disk space and maintain privacy"
arch=('any')
url="http://bleachbit.sourceforge.net/"
license=('GPL3')
depends=('pygtk>=2' 'gksu')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        http://downloads.sourceforge.net/$pkgname/$pkgname-bonus-$pkgver.tar.gz 
        $pkgname-admin.sh
        $pkgname-admin.png
        $pkgname-admin.desktop)
md5sums=('76181f2d89485d5ddadaec64ebff0ecb'
         'b4101e839faadf493af9c7a591b1e838'
         'a424bec443cc519391c837948fa98fdd'
         'ddd834baa40d481a603333638e735825'
         'fc13f064e8970af4418486469dce05d0')

build() {
    cd $srcdir/$pkgname-$pkgver

    # Compile
    make prefix=/usr DESTDIR=$pkgdir install || return 1

    # Bonus cleaners
    install -m 644 $srcdir/$pkgname-bonus-$pkgver/cleaners/*.xml $pkgdir/usr/share/$pkgname/cleaners/

    # Install bin, icon and desktop file
    install -Dm 755 $srcdir/$pkgname-admin.sh $pkgdir/usr/bin/$pkgname-admin
    install -Dm 644 $srcdir/$pkgname-admin.png $pkgdir/usr/share/pixmaps/$pkgname-admin.png
    install -Dm 644 $srcdir/$pkgname-admin.desktop $pkgdir/usr/share/applications/$pkgname-admin.desktop
}
