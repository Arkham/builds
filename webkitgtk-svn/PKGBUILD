# Maintainer: Arkham <arkham at archlinux dot us>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Anders Bergh <anders1@gmail.com>
# Contributor: rabyte <rabyte.at.pen.dot.tj>
# Contributor: N3ON <archlinux.at.alunamation.dot.com>
# fixes by sidious
# Contributor: hybraries <macwolf@archlinux.de>

pkgname=webkitgtk-svn
pkgver=46392
pkgrel=1
pkgdesc="An open source web browser engine"
arch=('i686' 'x86_64')
url="http://live.gnome.org/WebKitGtk"
license=('custom')
depends=('libxslt' 'libsoup-git' 'sqlite3' 'gtk2' 'icu' 'libxt' 'gstreamer0.10-base' 'enchant')
makedepends=('subversion' 'gperf' 'autoconf' 'bison' 'flex' 'gtk-doc')
provides=('webkitgtk' 'webkitgdk' 'webkitgdk-svn' 'libwebkit')
conflicts=('webkitgtk' 'webkitgdk' 'webkitgdk-svn' 'libwebkit')
options=('!libtool')
source=()
md5sums=()

_svntrunk=http://svn.webkit.org/repository/webkit/trunk
_svnmod=webkit

build() {
    cd "$srcdir"
    rm -rf "$srcdir/$_svnmod-build"

    if [ -d $_svnmod/.svn ]; then
        svn up $_svnmod -N -r $pkgver
    else
        svn co $_svntrunk $_svnmod -N -r $pkgver --config-dir ./
    fi

    for i in {autotools,JavaScript{Core,Glue},Web{Core,Kit{,Libraries,Tools}}}; do
        if [ -d $_svnmod/$i/.svn ]; then
            svn up $_svnmod/$i -r $pkgver
        else
            svn co $_svntrunk/$i $_svnmod/$i -r $pkgver --config-dir ./
        fi
    done

    msg "SVN checkout done or server timeout"
    msg "Copying data to builddir. This could take a while..."
    mkdir -p $_svnmod-build
    cp -rf $_svnmod/* $_svnmod-build/
    cd $_svnmod-build
    msg "Starting make..."

    ./autogen.sh --prefix=/usr \
    --enable-video \
    --with-http-backend=soup || return 1
    make || return 1
    make DESTDIR="$pkgdir" install || return 1

    install -Dm644 ${srcdir}/webkit-build/WebKit/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
