# Maintainer: Arkham <arkham at archlinux dot us>
# Contributor: rabyte <rabyte*gmail>
# Contributor: Andres Blanc <andresblanc@gmail.com>

pkgname=egoboo
pkgver=2.7.3
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="An open-source action RPG/dungeon crawling adventure with OpenGL 3D graphics"
url="http://egoboo.sourceforge.net/"
depends=('sdl_image' 'sdl_mixer' 'sdl_ttf' 'mesa')
conflicts=('egoboo-data')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        $pkgname.png
        $pkgname.desktop)
md5sums=('85ca96deed3403406be758223ec9c7ae'
         '7019f17b607fd45acf0d48ade746326a'
         'b81e0c8f3e0b9d8640d0cfc9b6d320a8')

build() {
    cd $srcdir/$pkgname-$pkgver/source/game

    # Patch, compile and install
    sed 's|${HOME}/.local|/usr|g' -i $pkgname.sh
    make OPT="${CFLAGS}" -f Makefile.unix || return 1
    make PREFIX=$pkgdir/usr -f Makefile.unix install || return 1

    cd $srcdir/$pkgname-$pkgver

    # Patch default depth to 24 and sound
    sed -i -e 's/\[COLOR_DEPTH\] : "32"/\[COLOR_DEPTH\] : "24"/g' \
        -e 's/\[Z_DEPTH\] : "32"/\[Z_DEPTH\] : "24"/g' \
        -e 's/\[OUTPUT_BUFFER_SIZE\] : "2548/\[OUTPUT_BUFFER_SIZE\] : "2048/g' \
        setup.txt    

    # Copy data and fix permissions
    install -d $pkgdir/usr/share/{doc/,$pkgname}
    cp -rf controls.txt setup.txt basicdat/ modules/ players/ $pkgdir/usr/share/$pkgname/
    cp -rf doc/ $pkgdir/usr/share/doc/$pkgname/
    find $pkgdir/usr/share/$pkgname -type f -exec chmod 644 {} +

    # Install icon and desktop files
    install -Dm 644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
    install -Dm 644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
