# Maintainer: Arkham <arkham at archlinux dot us>
# Contributor: Kaos <gianlucaatlas (at) gmail (dot) com>
# Contributor: Julius Bullinger <julius.bullinger@gmail.com>

pkgname=opera-devel-qt4
pkgver=10.00_4493
_build=4493
_realver=10.00-${_build}
pkgrel=1
pkgdesc="The Opera web browser development and testing version, shared Qt4 version"
arch=('i686' 'x86_64')
url="http://my.opera.com/desktopteam/blog"
license=('custom:opera')
depends=('qt')
conflicts=('opera' 'opera-devel' 'opera-static')
provides=('opera' 'opera-devel' 'opera-static')
source=(http://snapshot.opera.com/unix/snapshot-$_build/intel-linux/opera-$_realver.gcc4-qt4.i386.tar.bz2
        opera.desktop
        opera.svg
        opera-qt4-theme.patch)
[ "$CARCH" == "x86_64" ] && source[0]="http://snapshot.opera.com/unix/snapshot-$_build/x86_64-linux/opera-$_realver.gcc4-qt4.x86_64.tar.bz2"
md5sums=('8abb8763f0be89dda1c22aa71bd966f7'
         'e68398e5b8c4cb0b59fdb661ff1877e6'
         'bf26b6d02091786d4ed45f4874874e3a'
         'c42f31ccbc97304981d7ea38f87f5f43')
[ "$CARCH" == "x86_64" ] && md5sums[0]='6d626d9a7ec20671575bd6f7309c62cd'

build() {
    # Install the data and the license
    [ "$CARCH" == "i686" ] && cd $srcdir/opera-$_realver.gcc4-qt4.i386
    [ "$CARCH" == "x86_64" ] && cd $srcdir/opera-$_realver.gcc4-qt4.x86_64
    ./install.sh DESTDIR=$pkgdir || return 1
    install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/license.txt

    # Some patches: doing it here to prevent invalid checksums in install.sh
    cd $pkgdir/usr/bin && patch -i $srcdir/opera-qt4-theme.patch || return 1

    # Install .desktop and icon
    install -Dm 644 $srcdir/opera.desktop $pkgdir/usr/share/applications/opera.desktop
    install -Dm 644 $srcdir/opera.svg $pkgdir/usr/share/opera/images/opera.svg
}
